# VPC and network configuration

module "vpc" {
  source = "terraform-aws-modules/vpc/aws"

  name = "${var.project_name}-vpc"
  cidr = var.vpc_cidr

  azs            = var.availability_zones
  public_subnets = var.subnet_cidrs

  tags = {
    Name = "${var.project_name}-vpc"
  }
}

# Security group for Application Load Balancer

resource "aws_security_group" "alb_sg" {
  name        = "${var.project_name}-alb-sg"
  vpc_id      = module.vpc.vpc_id
  description = "Security group for Application Load Balancer"

  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = var.http_allowed_ips
    description = "HTTP access"
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks  = ["0.0.0.0/0"]
    description = "Allow all outbound traffic"
  }

  tags = {
    Name = "${var.project_name}-alb-sg"
  }
}

# Security group for Ollama EC2 instance

resource "aws_security_group" "ollama_sg" {
  name        = "${var.project_name}-ollama-sg"
  vpc_id      = module.vpc.vpc_id
  description = "Security group for Ollama EC2 instance"
  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = var.ssh_allowed_ips
    description = "SSH access"
  }

  ingress {
    from_port   = 11434
    to_port     = 11434
    protocol    = "tcp"
    security_groups = [aws_security_group.alb_sg.id]
    description = "Ollama API access"
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
    description = "Allow all outbound traffic"
  }

  tags = {
    Name = "${var.project_name}-ollama-sg"
  }
}

# Security group for Web UI EC2 instance
resource "aws_security_group" "webui_sg" {
  name        = "${var.project_name}-ui-sg"
  vpc_id      = module.vpc.vpc_id
  description = "Security group for Web UI EC2 instance"
  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = var.ssh_allowed_ips
    description = "SSH access"
  }
  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = var.http_allowed_ips
    description = "HTTP access"
  }
  ingress {
    from_port   = 11434
    to_port     = 11434
    protocol    = "tcp"
    security_groups = [aws_security_group.ollama_sg.id]
    description = "Ollama API access"
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
    description = "Allow all outbound traffic"
  }
  tags = {
    Name = "${var.project_name}-ui-sg"
  }
}


